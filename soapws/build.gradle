plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.spr.soapws'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

configurations {
	jaxb
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-web-services'
	implementation 'wsdl4j:wsdl4j'
	jaxb("org.glassfish.jaxb:jaxb-xjc")
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			srcDir 'src/generated-sources/jaxb'
		}
	}
}

task genJaxb {
	ext.sourcesDir = "src/generated-sources/jaxb"
	ext.schema = "src/main/resources/xsds/schema.xsd"
	ext.packageName = "com.spr.soapws"
	outputs.dir sourcesDir

	doLast() {
		project.ant {
			taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
					classpath: configurations.jaxb.asPath
			mkdir(dir: sourcesDir)
			xjc(destdir: sourcesDir, schema: schema, package: packageName) {
				arg(value: "-wsdl")
				produces(dir: sourcesDir, includes: "**/*.java")
			}
		}
	}
}

compileJava.dependsOn genJaxb
tasks.named('test') {
	useJUnitPlatform()
}
